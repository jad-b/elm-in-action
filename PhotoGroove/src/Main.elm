module Main exposing (main)

import Browser exposing (Document)
import Browser.Navigation as Nav
import Html exposing (Html, a, footer, h1, li, nav, text, ul)
import Html.Attributes exposing (classList, href)
import Html.Lazy exposing (lazy)
import PhotoFolders as Folders
import PhotoGallery as Gallery
import Url exposing (Url)
import Url.Parser as Parser exposing ((</>), Parser, s, string)
import Json.Encode exposing (float)


type alias Model =
    { page : Page
    , key : Nav.Key
    , pastaVersion : Float
    }


type Page
    = GalleryPage Gallery.Model
    | FoldersPage Folders.Model
    | NotFound


type Route
    = Gallery
    | Folders
    | SelectedPhoto String


view : Model -> Document Msg
view model =
    let
        content =
            text "This isn't even my final form!"
    in
    { title = "Photo Groove, SPA Style"
    , body =
        [ lazy viewHeader model.page
        , content
        , viewFooter
        ]
    }


viewHeader : Page -> Html Msg
viewHeader page =
    let
        logo =
            h1 [] [ text "Photo Groove" ]

        links =
            ul []
                [ navLink Folders { url = "/", caption = "Folders" }
                , navLink Gallery { url = "/gallery", caption = "Gallery" }
                ]

        navLink : Route -> { url : String, caption : String } -> Html msg
        navLink route { url, caption } =
            li
                [ classList
                    [ ( "active", isActive { link = route, page = page } )
                    ]
                ]
                [ a [ href url ] [ text caption ] ]
    in
    nav [] [ logo, links ]


isActive : { link : Route, page : Page } -> Bool
isActive { link, page } =
    case ( link, page ) of
        ( Gallery, GalleryPage _ ) ->
            True

        ( Gallery, _ ) ->
            False

        ( Folders, FoldersPage _ ) ->
            True

        ( Folders, _ ) ->
            False

        ( SelectedPhoto _, _ ) ->
            False


viewFooter : Html msg
viewFooter =
    footer [] [ text "One is never alone with a rubber duck. - Douglas Adams" ]


type Msg
    = ClickedLink Browser.UrlRequest
    | ChangedUrl Url


update : Msg -> Model -> ( Model, Cmd Msg )
update msg model =
    case msg of
        ClickedLink (Browser.External href) ->
            -- Load the new page
            ( model, Nav.load href )

        ClickedLink (Browser.Internal url) ->
            ( model, Nav.pushUrl model.key (Url.toString url) )

        ChangedUrl url ->
            ( { model | page = urlToPage model.pastaVersion url }, Cmd.none )


subscriptions : Model -> Sub Msg
subscriptions model =
    Sub.none


init : Float -> Url -> Nav.Key -> ( Model, Cmd Msg )
init pastaVersion url key =
    -- 'key' is an interesting use of types to softly guarantee correctness
    -- Nav.pushUrl requires a Nav.Key
    -- A Nav.Key is only generated by init - but not just any init
    -- Only the type of 'init' taken by Browser.application gives you a Nav.Key
    -- q.e.d Only Browser.applications can hijack navigation
    ( { page = urlToPage pastaVersion url, key = key, pastaVersion = pastaVersion }
    , Cmd.none 
    )


urlToPage : Float -> Url -> Page
urlToPage pastaVersion url =
    case Parser.parse pageParser url of
        Just Gallery ->
            GalleryPage (Tuple.first (Gallery.init (float pastaVersion) ))

        Just Folders ->
            FoldersPage (Tuple.first (Folders.init (Nothing)))

        Just (SelectedPhoto filename) ->
            -- TODO Make use of filename
            FoldersPage (Tuple.first (Folders.init (Just filename)))

        Nothing ->
            NotFound


pageParser : Parser (Route -> a) a
pageParser =
    Parser.oneOf
        [ Parser.map Folders Parser.top
        , Parser.map Gallery (s "gallery")
        , Parser.map SelectedPhoto (s "photos" </> Parser.string)
        ]


main : Program Float Model Msg
main =
    Browser.application
        { init = init
        , onUrlRequest = ClickedLink
        , onUrlChange = ChangedUrl
        , subscriptions = subscriptions
        , update = update

        -- Browser.element.view must return Html Msg
        -- Browser.document.view must return Document Msg
        -- Document gives Elm access to the entire web page, instead of a
        -- (parent) DOM node
        , view = view
        }
